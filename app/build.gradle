plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    // Compose Plugin only available starting Kotlin 2.0+
    // alias(libs.plugins.kotlin.compose)
}

android {
    namespace = "io.r_a_d.radio2"
    signingConfigs {
        production {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            storeFile file(rootProject.projectDir.absolutePath + keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    compileSdk 35
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18
    }
    kotlinOptions {
        jvmTarget = "18"
    }
    defaultConfig {
        applicationId "io.r_a_d.radio2"
        minSdkVersion 21        // API 21 is the minimum for media3 support.
        //noinspection EditedTargetSdkVersion
        targetSdkVersion 36
        versionCode 25042604
        multiDexEnabled true
        versionName "2.4.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildFeatures {
        buildConfig true
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            debuggable false
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "androidx") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "${targetSdk}.+"
            }
        }
    }
}

dependencies {

    testImplementation libs.junit
    implementation libs.androidx.junit
    implementation libs.multidex
    implementation libs.androidx.media
    implementation libs.androidx.media3.exoplayer
    implementation libs.androidx.legacy
    implementation libs.androidx.lifecycle.extensions
    implementation libs.androidx.lifecycle.viewmodel.ktx

    implementation libs.androidx.recyclerview
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.work.runtime.ktx
    implementation libs.androidx.activity.ktx

    // Note: updating androidx.fragment:fragment-ktx to 1.5.0 and up makes the app logo
    // imprinted over every fragment, for some reason. I would have to read the changelog for 1.5.0
    // to understand why this happens and how to fix it. One day.
    // TODO: must debug this
    implementation libs.androidx.fragment.ktx

    implementation libs.kotlin.stdlib
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.core.ktx

    implementation libs.androidx.constraintlayout
    implementation libs.androidx.core.runtime

    // Navigation
    implementation libs.androidx.navigation.runtime.ktx
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx
    implementation libs.glide

}
